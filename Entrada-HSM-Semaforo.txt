[*] -> fn :

state fn {
	[*] -> fn_sem1 :
	-> modonot : EVENT_TROCA_MODO
	
	state fn_sem1 {
		[*] -> fn_sem1_verde1 : / "acende_verde1()"
		
		state fn_sem1_verde1 {
			-> fn_sem1_verde_amb1 : EVENT_TIMEOUT10 ["amb1_presente()"]
			-> fn_sem1_amarelo : EVENT_TIMEOUT10 ["amb2_presente()"] / "acende_amarelo1()"
			-> fn_sem1_verde2 : EVENT_TIMEOUT10
		}
		
		state fn_sem1_verde2 {
			-> fn_sem1_verde_amb1 : EVENT_AMB1
			-> fn_sem1_amarelo : EVENT_AMB2 / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_TIMEOUT30 ["carro2_presente()"] / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_TIMEOUT30 ["pedestre_presente()"] / "acende_amarelo1()"
			-> fn_sem1_verde3 : EVENT_TIMEOUT30
		}
		
		state fn_sem1_verde3 {
			-> fn_sem1_verde_amb1 : EVENT_AMB1
			-> fn_sem1_amarelo : EVENT_AMB2 / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_PEDESTRE / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_CARRO2 / "acende_amarelo1()"
		}
		
		state fn_sem1_verde_amb1{
			-> fn_sem1_amarelo : EVENT_AMB1_LEFT ["amb2_presente()"] / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_AMB1_LEFT ["carro2_presente()"] / "acende_amarelo1()"
			-> fn_sem1_amarelo : EVENT_AMB1_LEFT ["pedestre_presente()"] / "acende_amarelo1()"
			-> fn_sem1_verde3 : EVENT_AMB1_LEFT
		}
		
		state fn_sem1_amarelo {
			-> fn_sem2_verde_amb2 : EVENT_TIMEOUT5 ["amb2_presente()"] / "acende_verde2()"
			-> fn_sem2 : EVENT_TIMEOUT5 ["carro2_presente()"] / "acende_verde2()"
			-> fn_sem3 : EVENT_TIMEOUT5 ["pedestre_presente()"] / "acende_verde3()"
			-> fn_sem2 : EVENT_TIMEOUT5 / "acende_verde2()"
		}

	}
	
	state fn_sem2 {
		[*] -> fn_sem2_verde1 :  / "acende_verde2()"
		
		state fn_sem2_verde1 {
			-> fn_sem2_verde_amb2 : EVENT_TIMEOUT10 ["amb2_presente()"]
			-> fn_sem2_verde2 : EVENT_TIMEOUT10
		}
		
		state fn_sem2_verde2 {
			-> fn_sem2_verde_amb2 : EVENT_AMB2
			-> fn_sem2_amarelo : EVENT_AMB1 / "acende_amarelo2()"
			-> fn_sem2_amarelo : EVENT_TIMEOUT30 / "acende_amarelo2()"
		}
		
		state fn_sem2_verde_amb2 {
			-> fn_sem2_verde2 : EVENT_AMB2_LEFT
		}
		
		state fn_sem2_amarelo {
			-> fn_sem1_verde_amb1 : EVENT_TIMEOUT5 ["amb2_presente()"] / "acende_verde1()"
			-> fn_sem1 : EVENT_TIMEOUT5 / "acende_verde1()"
		}
		
	}
	
	state fn_sem3 {
		[*] -> fn_sem3_verde : / "acende_verde3()"
		
		state fn_sem3_verde {
			-> fn_sem3_pisca : EVENT_TIMEOUT20
		}
		
		state fn_sem3_pisca {
			[*] -> fn_sem3_pisca_on :
			-> fn_sem1_verde_amb1 : EVENT_TIMEOUT10 ["amb1_presente()"]
			-> fn_sem2_verde_amb2 : EVENT_TIMEOUT10 ["amb2_presente()"]
			-> fn_sem2 : EVENT_TIMEOUT10 ["carro2_presente()"]
			-> fn_sem1 : EVENT_TIMEOUT10
			
			state fn_sem3_pisca_on {
				-> fn_sem3_pisca_off : EVENT_TIMEOUT1 / "apaga_verde3()"
			}
			
			state fn_sem3_pisca_off {
				-> fn_sem3_pisca_on : EVENT_TIMEOUT1 / "acende_verde3()"
			}
		}
	}
}

state modonot {
	[*] -> modonot_amarelos_on : / "acende_amarelos_not()"
	-> fn : EVENT_TROCA_MODO
	
	state modonot_amarelos_on {
		-> modonot_amarelos_off : EVENT_TIMEOUT1 / "apaga_amarelos_not()"
	}
	
	state modonot_amarelos_off {
		-> modonot_amarelos_on : EVENT_TIMEOUT1 / "acende_amarelos_not()"
	}
	
}
